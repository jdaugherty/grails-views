def groovyVersion = project.rootProject
        .extensions
        .getByType(VersionCatalogsExtension.class)
        .named("libs")
        .findVersion("groovy")
        .get()
        .displayName

def micronautVersion = project.rootProject
        .extensions
        .getByType(VersionCatalogsExtension.class)
        .named("libs")
        .findVersion("micronaut")
        .get()
        .displayName

allprojects {
    configurations.configureEach {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if ((details.requested.group == 'org.codehaus.groovy' || details.requested.group == 'org.apache.groovy') && details.requested.name != 'groovy-bom') {
                details.useTarget(group: 'org.apache.groovy', name: details.requested.name, version: groovyVersion)
                details.because "The dependency coordinates are changed in Apache Groovy 4, plus ensure version"
            }

            if (details.requested.group == "io.micronaut" && details.requested.name == "micronaut-inject-groovy") {
                details.useVersion(micronautVersion)
            }
        }
    }
}