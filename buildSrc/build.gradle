plugins {
    id 'groovy-gradle-plugin'
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.grails.org/grails/core' }

    if (System.getenv("GITHUB_MAVEN_PASSWORD")) {
        System.out.println("Adding Grails Core Repo")
        maven {
            url = 'https://maven.pkg.github.com/grails/grails-core'
            credentials {
                username = 'DOES_NOT_MATTER'
                password = System.getenv("GITHUB_MAVEN_PASSWORD")
            }
        }
    }
}

file("../gradle.properties").withReader {
    Properties props = new Properties()
    props.load(it)
    project.ext.gradleProperties = props
}

dependencies {
    // Needs to be implementation, as Asciidoctor task is referenced in docs/build.gradle
    implementation "org.asciidoctor:asciidoctor-gradle-jvm:${gradleProperties.asciidoctorVersion}"
    runtimeOnly "org.grails:grails-gradle-plugin:${gradleProperties.grailsGradlePluginVersion}"
    runtimeOnly "org.grails.plugins:views-gradle:${gradleProperties.projectVersion}"
}
