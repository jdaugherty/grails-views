plugins {
    id 'java-library'
    id 'groovy'
    id 'org.grails.grails-plugin'
    id 'maven-publish'
    id 'signing'
}

group = 'org.grails.plugins'
ext.set('projectDesc', 'Grails JSON Views')
ext.set('isGrailsPlugin', true)

dependencies {
    api project(':views-core') // Used in public API
    api 'org.grails:grails-plugin-rest' // Used in public API

    implementation 'org.grails:grails-encoder'
    implementation 'org.apache.groovy:groovy'
    implementation 'org.apache.groovy:groovy-json'
    implementation 'jakarta.validation:jakarta.validation-api'

    testImplementation project(':views-json-testing-support')
    testImplementation 'org.grails:grails-testing-support'
    testImplementation 'org.grails:grails-gorm-testing-support'
    testImplementation 'org.grails:grails-datastore-gorm-hibernate5'
    testImplementation 'org.spockframework:spock-core'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'jakarta.servlet:jakarta.servlet-api'

    testRuntimeOnly 'org.slf4j:slf4j-nop' // Get rid of warning about missing slf4j implementation during test task
}

apply from: rootProject.layout.projectDirectory.file('gradle/grails-plugin-config.gradle')
apply from: rootProject.layout.projectDirectory.file('gradle/java-config.gradle')
apply from: rootProject.layout.projectDirectory.file('gradle/api-docs-config.gradle')
apply from: rootProject.layout.projectDirectory.file('gradle/publishing.gradle')

def testArtifacts = ['*.gson', 'circular/**']

tasks.named('jar', Jar).configure {
    it.exclude testArtifacts
}
tasks.named('sourcesJar', Jar).configure {
    it.exclude testArtifacts
}
