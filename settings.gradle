plugins {
    id 'com.gradle.develocity' version '3.19'
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '2.0.2'
}

def isCI = System.getenv('CI') != null
def isLocal = !isCI
def isAuthenticated = System.getenv('DEVELOCITY_ACCESS_KEY') != null

develocity {
    server = 'https://ge.grails.org'
    buildScan {
        tag('grails')
        tag('grails-views')
        publishing.onlyIf { isAuthenticated }
        uploadInBackground = isLocal
    }
}

buildCache {
    local { enabled = isLocal }
    remote(develocity.buildCache) {
        push = isCI && isAuthenticated
        enabled = true
    }
}

rootProject.name = 'grails-views'

['core', 'json', 'docs', 'json-testing-support', 'markup'].each {dirName ->
    include("views-$dirName")
    project(":views-$dirName").projectDir = file(dirName)
}

include 'views-gradle'
project(':views-gradle').projectDir = file('gradle-plugin')

file('examples').eachDir { dir ->
    include "examples-$dir.name"
    project(":examples-$dir.name").projectDir = dir
}
